<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html>
<!-- Created by GNU Texinfo 6.8, https://www.gnu.org/software/texinfo/ -->
<head>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<title>Derived expression types formal (Revised(7) Report on the Algorithmic Language Scheme)</title>

<meta name="description" content="Derived expression types formal (Revised(7) Report on the Algorithmic Language Scheme)">
<meta name="keywords" content="Derived expression types formal (Revised(7) Report on the Algorithmic Language Scheme)">
<meta name="resource-type" content="document">
<meta name="distribution" content="global">
<meta name="Generator" content="makeinfo">
<meta name="viewport" content="width=device-width,initial-scale=1">

<link href="index.html" rel="start" title="top">
<link href="Alphabetic-index.html" rel="index" title="Alphabetic index">
<link href="Formal-syntax-and-semantics.html" rel="up" title="Formal syntax and semantics">
<link href="Formal-semantics.html" rel="prev" title="Formal semantics">
<style type="text/css">
<!--
a.copiable-anchor {visibility: hidden; text-decoration: none; line-height: 0em}
a.summary-letter {text-decoration: none}
blockquote.indentedblock {margin-right: 0em}
div.display {margin-left: 3.2em}
div.example {margin-left: 3.2em}
kbd {font-style: oblique}
pre.display {font-family: inherit}
pre.format {font-family: inherit}
pre.menu-comment {font-family: serif}
pre.menu-preformatted {font-family: serif}
span.nolinebreak {white-space: nowrap}
span.roman {font-family: initial; font-weight: normal}
span.sansserif {font-family: sans-serif; font-weight: normal}
span:hover a.copiable-anchor {visibility: visible}
ul.no-bullet {list-style: none}
-->
</style>


</head>

<body lang="en">
<div class="section" id="Derived-expression-types-formal">
<div class="header">
<p>
Previous: <a href="Formal-semantics.html" accesskey="p" rel="prev">Formal semantics</a>, Up: <a href="Formal-syntax-and-semantics.html" accesskey="u" rel="up">Formal syntax and semantics</a> &nbsp; [<a href="Alphabetic-index.html" title="Index" rel="index">Index</a>]</p>
</div>
<hr>
<span id="Derived-expression-types-2"></span><h3 class="section">7.3 Derived expression types</h3>

<p>This section gives syntax definitions for the derived expression
types in terms of the primitive expression types (literal, variable,
call, <code>lambda</code>, <code>if</code>, and <code>set!</code>), except for
<code>quasiquote</code>.
</p>
<p>Conditional derived syntax types:
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax cond
  (syntax-rules (else =&gt;)
    ((cond (else result1 result2 ...))
     (begin result1 result2 ...))
    ((cond (test =&gt; result))
     (let ((temp test))
       (if temp (result temp))))
    ((cond (test =&gt; result) clause1 clause2 ...)
     (let ((temp test))
       (if temp
           (result temp)
           (cond clause1 clause2 ...))))
    ((cond (test)) test)
    ((cond (test) clause1 clause2 ...)
     (let ((temp test))
       (if temp
           temp
           (cond clause1 clause2 ...))))
    ((cond (test result1 result2 ...))
     (if test (begin result1 result2 ...)))
    ((cond (test result1 result2 ...)
           clause1 clause2 ...)
     (if test
         (begin result1 result2 ...)
         (cond clause1 clause2 ...)))))

(define-syntax case
  (syntax-rules (else =&gt;)
    ((case (key ...)
       clauses ...)
     (let ((atom-key (key ...)))
       (case atom-key clauses ...)))
    ((case key
       (else =&gt; result))
     (result key))
    ((case key
       (else result1 result2 ...))
     (begin result1 result2 ...))
    ((case key
       ((atoms ...) =&gt; result))
     (if (memv key '(atoms ...))
         (result key)))
    ((case key
       ((atoms ...) result1 result2 ...))
     (if (memv key '(atoms ...))
         (begin result1 result2 ...)))
    ((case key
       ((atoms ...) =&gt; result)
       clause clauses ...)
     (if (memv key '(atoms ...))
         (result key)
         (case key clause clauses ...)))
    ((case key
       ((atoms ...) result1 result2 ...)
       clause clauses ...)
     (if (memv key '(atoms ...))
         (begin result1 result2 ...)
         (case key clause clauses ...)))))

(define-syntax and
  (syntax-rules ()
    ((and) #t)
    ((and test) test)
    ((and test1 test2 ...)
     (if test1 (and test2 ...) #f))))

(define-syntax or
  (syntax-rules ()
    ((or) #f)
    ((or test) test)
    ((or test1 test2 ...)
     (let ((x test1))
       (if x x (or test2 ...))))))

(define-syntax when
  (syntax-rules ()
    ((when test result1 result2 ...)
     (if test
         (begin result1 result2 ...)))))

(define-syntax unless
  (syntax-rules ()
    ((unless test result1 result2 ...)
     (if (not test)
         (begin result1 result2 ...)))))
</pre></div>

<p>Binding constructs:
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax let
  (syntax-rules ()
    ((let ((name val) ...) body1 body2 ...)
     ((lambda (name ...) body1 body2 ...)
      val ...))
    ((let tag ((name val) ...) body1 body2 ...)
     ((letrec ((tag (lambda (name ...)
                      body1 body2 ...)))
        tag)
      val ...))))

(define-syntax let*
  (syntax-rules ()
    ((let* () body1 body2 ...)
     (let () body1 body2 ...))
    ((let* ((name1 val1) (name2 val2) ...)
       body1 body2 ...)
     (let ((name1 val1))
       (let* ((name2 val2) ...)
         body1 body2 ...)))))
</pre></div>

<p>The following <code>letrec</code> macro uses the symbol <code>&lt;undefined&gt;</code>
in place of an expression which returns something that when stored in
a location makes it an error to try to obtain the value stored in the
location. (No such expression is defined in Scheme.) A trick is used
to generate the temporary names needed to avoid specifying the order
in which the values are evaluated. This could also be accomplished
by using an auxiliary macro.
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax letrec
  (syntax-rules ()
    ((letrec ((var1 init1) ...) body ...)
     (letrec &quot;generate_temp_names&quot;
       (var1 ...)
       ()
       ((var1 init1) ...)
       body ...))
    ((letrec &quot;generate_temp_names&quot;
       ()
       (temp1 ...)
       ((var1 init1) ...)
       body ...)
     (let ((var1 &lt;undefined&gt;) ...)
       (let ((temp1 init1) ...)
         (set! var1 temp1)
         ...
         body ...)))
    ((letrec &quot;generate_temp_names&quot;
       (x y ...)
       (temp ...)
       ((var1 init1) ...)
       body ...)
     (letrec &quot;generate_temp_names&quot;
       (y ...)
       (newtemp temp ...)
       ((var1 init1) ...)
       body ...))))

(define-syntax letrec*
  (syntax-rules ()
    ((letrec* ((var1 init1) ...) body1 body2 ...)
     (let ((var1 &lt;undefined&gt;) ...)
       (set! var1 init1)
       ...
       (let () body1 body2 ...)))))

(define-syntax let-values
  (syntax-rules ()
    ((let-values (binding ...) body0 body1 ...)
     (let-values &quot;bind&quot;
       (binding ...) () (begin body0 body1 ...)))

    ((let-values &quot;bind&quot; () tmps body)
     (let tmps body))

    ((let-values &quot;bind&quot; ((b0 e0)
                         binding ...) tmps body)
     (let-values &quot;mktmp&quot; b0 e0 ()
                 (binding ...) tmps body))

    ((let-values &quot;mktmp&quot; () e0 args
                 bindings tmps body)
     (call-with-values
         (lambda () e0)
       (lambda args
         (let-values &quot;bind&quot;
           bindings tmps body))))

    ((let-values &quot;mktmp&quot; (a . b) e0 (arg ...)
                 bindings (tmp ...) body)
     (let-values &quot;mktmp&quot; b e0 (arg ... x)
                 bindings (tmp ... (a x)) body))

    ((let-values &quot;mktmp&quot; a e0 (arg ...)
                 bindings (tmp ...) body)
     (call-with-values
         (lambda () e0)
       (lambda (arg ... . x)
         (let-values &quot;bind&quot;
           bindings (tmp ... (a x)) body))))))

(define-syntax let*-values
  (syntax-rules ()
    ((let*-values () body0 body1 ...)
     (let () body0 body1 ...))

    ((let*-values (binding0 binding1 ...)
       body0 body1 ...)
     (let-values (binding0)
       (let*-values (binding1 ...)
         body0 body1 ...)))))

(define-syntax define-values
  (syntax-rules ()
    ((define-values () expr)
     (define dummy
       (call-with-values (lambda () expr)
         (lambda args #f))))
    ((define-values (var) expr)
     (define var expr))
    ((define-values (var0 var1 ... varn) expr)
     (begin
       (define var0
         (call-with-values (lambda () expr)
           list))
       (define var1
         (let ((v (cadr var0)))
           (set-cdr! var0 (cddr var0))
           v)) ...
           (define varn
             (let ((v (cadr var0)))
               (set! var0 (car var0))
               v))))
    ((define-values (var0 var1 ... . varn) expr)
     (begin
       (define var0
         (call-with-values (lambda () expr)
           list))
       (define var1
         (let ((v (cadr var0)))
           (set-cdr! var0 (cddr var0))
           v)) ...
           (define varn
             (let ((v (cdr var0)))
               (set! var0 (car var0))
               v))))
    ((define-values var expr)
     (define var
       (call-with-values (lambda () expr)
         list)))))

(define-syntax begin
  (syntax-rules ()
    ((begin exp ...)
     ((lambda () exp ...)))))
</pre></div>

<p>The following alternative expansion for <code>begin</code> does not make
use of the ability to write more than one expression in the body of
a lambda expression. In any case, note that these rules apply only
if the body of the <code>begin</code> contains no definitions.
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax begin
  (syntax-rules ()
    ((begin exp)
     exp)
    ((begin exp1 exp2 ...)
     (call-with-values
         (lambda () exp1)
       (lambda args
         (begin exp2 ...))))))
</pre></div>

<p>The following syntax definition of do uses a trick to expand the
variable clauses. As with <code>letrec</code> above, an auxiliary macro
would also work. The expression <code>(if #f #f)</code> is used to obtain
an unspecific value.
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax do
  (syntax-rules ()
    ((do ((var init step ...) ...)
         (test expr ...)
       command ...)
     (letrec
         ((loop
           (lambda (var ...)
             (if test
                 (begin
                   (if #f #f)
                   expr ...)
                 (begin
                   command
                   ...
                   (loop (do &quot;step&quot; var step ...)
                         ...))))))
       (loop init ...)))
    ((do &quot;step&quot; x)
     x)
    ((do &quot;step&quot; x y)
     y)))
</pre></div>


<p>Here is a possible implementation of <code>delay</code>, <code>force</code> and
<code>delay-force</code>. We define the expression
</p>
<div class="example lisp">
<pre class="lisp">(delay-force &#x27e8;expression&#x27e9;)
</pre></div>

<p>to have the same meaning as the procedure call
</p>
<div class="example lisp">
<pre class="lisp">(make-promise #f (lambda () &#x27e8;expression&#x27e9;))
</pre></div>

<p>as follows
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax delay-force
  (syntax-rules ()
    ((delay-force expression)
     (make-promise #f (lambda () expression)))))
</pre></div>

<p>and we define the expression
</p>
<div class="example lisp">
<pre class="lisp">(delay &#x27e8;expression&#x27e9;)
</pre></div>

<p>to have the same meaning as:
</p>
<div class="example lisp">
<pre class="lisp">(delay-force (make-promise #t &#x27e8;expression&#x27e9;))
</pre></div>

<p>as follows
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax delay
  (syntax-rules ()
    ((delay expression)
     (delay-force (make-promise #t expression)))))
</pre></div>

<p>where <code>make-promise</code> is defined as follows:
</p>
<div class="example lisp">
<pre class="lisp">(define make-promise
  (lambda (done? proc)
    (list (cons done? proc))))
</pre></div>

<p>Finally, we define <code>force</code> to call the procedure expressions
in promises iteratively using a trampoline technique following SRFI
45 [<a href="References.html#srfi45">srfi45</a>] until a non-lazy result (i.e. a value created by <code>delay</code>
instead of <code>delay-force</code>) is returned, as follows:
</p>
<div class="example lisp">
<pre class="lisp">(define (force promise)
  (if (promise-done? promise)
      (promise-value promise)
      (let ((promise* ((promise-value promise))))
        (unless (promise-done? promise)
          (promise-update! promise* promise))
        (force promise))))
</pre></div>

<p>with the following promise accessors:
</p>
<div class="example lisp">
<pre class="lisp">(define promise-done?
  (lambda (x) (car (car x))))
(define promise-value
  (lambda (x) (cdr (car x))))
(define promise-update!
  (lambda (new old)
    (set-car! (car old) (promise-done? new))
    (set-cdr! (car old) (promise-value new))
    (set-car! new (car old))))
</pre></div>

<p>The following implementation of <code>make-parameter</code> and
<code>parameterize</code> is suitable for an implementation with no
threads. Parameter objects are implemented here as procedures,
using two arbitrary unique objects &#x27e8;param-set!&#x27e9; and
&#x27e8;param-convert&#x27e9;:
</p>
<div class="example lisp">
<pre class="lisp">(define (make-parameter init . o)
  (let* ((converter
          (if (pair? o) (car o) (lambda (x) x)))
         (value (converter init)))
    (lambda args
      (cond
       ((null? args)
        value)
       ((eq? (car args) &#x27e8;param-set!&#x27e9;)
        (set! value (cadr args)))
       ((eq? (car args) &#x27e8;param-convert&#x27e9;)
        converter)
       (else
        (error &quot;bad parameter syntax&quot;))))))
</pre></div>

<p>Then <code>parameterize</code> uses <code>dynamic-wind</code> to
dynamically rebind the associated value:
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax parameterize
  (syntax-rules ()
    ((parameterize (&quot;step&quot;)
                   ((param value p old new) ...)
                   ()
                   body)
     (let ((p param) ...)
       (let ((old (p)) ...
             (new ((p &#x27e8;param-convert&#x27e9;) value)) ...)
         (dynamic-wind
          (lambda () (p &#x27e8;param-set!&#x27e9; new) ...)
          (lambda () . body)
          (lambda () (p &#x27e8;param-set!&#x27e9; old) ...)))))
    ((parameterize (&quot;step&quot;)
                   args
                   ((param value) . rest)
                   body)
     (parameterize (&quot;step&quot;)
                   ((param value p old new) . args)
                   rest
                   body))
    ((parameterize ((param value) ...) . body)
     (parameterize (&quot;step&quot;)
                   ()
                   ((param value) ...)
                   body))))
</pre></div>

<p>The following implementation of <code>guard</code> depends on an auxiliary
macro, here called <code>guard-aux</code>.
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax guard
  (syntax-rules ()
    ((guard (var clause ...) e1 e2 ...)
     ((call/cc
       (lambda (guard-k)
         (with-exception-handler
          (lambda (condition)
            ((call/cc
               (lambda (handler-k)
                 (guard-k
                  (lambda ()
                    (let ((var condition))
                      (guard-aux
                        (handler-k
                          (lambda ()
                            (raise-continuable condition)))
                        clause ...))))))))
          (lambda ()
            (call-with-values
             (lambda () e1 e2 ...)
             (lambda args
               (guard-k
                 (lambda ()
                   (apply values args)))))))))))))

(define-syntax guard-aux
  (syntax-rules (else =&gt;)
    ((guard-aux reraise (else result1 result2 ...))
     (begin result1 result2 ...))
    ((guard-aux reraise (test =&gt; result))
     (let ((temp test))
       (if temp
           (result temp)
           reraise)))
    ((guard-aux reraise (test =&gt; result)
                clause1 clause2 ...)
     (let ((temp test))
       (if temp
           (result temp)
           (guard-aux reraise clause1 clause2 ...))))
    ((guard-aux reraise (test))
     (or test reraise))
    ((guard-aux reraise (test) clause1 clause2 ...)
     (let ((temp test))
       (if temp
           temp
           (guard-aux reraise clause1 clause2 ...))))
    ((guard-aux reraise (test result1 result2 ...))
     (if test
         (begin result1 result2 ...)
         reraise))
    ((guard-aux reraise
                (test result1 result2 ...)
                clause1 clause2 ...)
     (if test
         (begin result1 result2 ...)
         (guard-aux reraise clause1 clause2 ...)))))

(define-syntax case-lambda
  (syntax-rules ()
    ((case-lambda (params body0 ...) ...)
     (lambda args
       (let ((len (length args)))
         (letrec-syntax
             ((cl (syntax-rules ::: ()
                    ((cl)
                     (error &quot;no matching clause&quot;))
                    ((cl ((p :::) . body) . rest)
                     (if (= len (length '(p :::)))
                         (apply (lambda (p :::)
                                  . body)
                                args)
                         (cl . rest)))
                    ((cl ((p ::: . tail) . body)
                         . rest)
                     (if (&gt;= len (length '(p :::)))
                         (apply
                          (lambda (p ::: . tail)
                            . body)
                          args)
                         (cl . rest))))))
           (cl (params body0 ...) ...)))))))
</pre></div>

<p>This definition of <code>cond-expand</code> does not interact with the
<code>features</code> procedure. It requires that each feature identifier
provided by the implementation be explicitly mentioned.
</p>
<div class="example lisp">
<pre class="lisp">(define-syntax cond-expand
  ;; Extend this to mention all feature ids and libraries
  (syntax-rules (and or not else r7rs library scheme base)
    ((cond-expand)
     (syntax-error &quot;Unfulfilled cond-expand&quot;))
    ((cond-expand (else body ...))
     (begin body ...))
    ((cond-expand ((and) body ...) more-clauses ...)
     (begin body ...))
    ((cond-expand ((and req1 req2 ...) body ...)
                  more-clauses ...)
     (cond-expand
       (req1
         (cond-expand
           ((and req2 ...) body ...)
           more-clauses ...))
       more-clauses ...))
    ((cond-expand ((or) body ...) more-clauses ...)
     (cond-expand more-clauses ...))
    ((cond-expand ((or req1 req2 ...) body ...)
                  more-clauses ...)
     (cond-expand
       (req1
        (begin body ...))
       (else
        (cond-expand
           ((or req2 ...) body ...)
           more-clauses ...))))
    ((cond-expand ((not req) body ...)
                  more-clauses ...)
     (cond-expand
       (req
         (cond-expand more-clauses ...))
       (else body ...)))
    ((cond-expand (r7rs body ...)
                  more-clauses ...)
       (begin body ...))
    ;; Add clauses here for each
    ;; supported feature identifier.
    ;; Samples:
    ;; ((cond-expand (exact-closed body ...)
    ;;               more-clauses ...)
    ;;   (begin body ...))
    ;; ((cond-expand (ieee-float body ...)
    ;;               more-clauses ...)
    ;;   (begin body ...))
    ((cond-expand ((library (scheme base))
                   body ...)
                  more-clauses ...)
      (begin body ...))
    ;; Add clauses here for each library
    ((cond-expand (feature-id body ...)
                  more-clauses ...)
       (cond-expand more-clauses ...))
    ((cond-expand ((library (name ...))
                   body ...)
                  more-clauses ...)
       (cond-expand more-clauses ...))))
</pre></div>

</div>
<hr>
<div class="header">
<p>
Previous: <a href="Formal-semantics.html">Formal semantics</a>, Up: <a href="Formal-syntax-and-semantics.html">Formal syntax and semantics</a> &nbsp; [<a href="Alphabetic-index.html" title="Index" rel="index">Index</a>]</p>
</div>



</body>
</html>
